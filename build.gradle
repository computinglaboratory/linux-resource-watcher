plugins {
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.sonatype.gradle.plugins.scan' version '2.5.5'
}

group 'io.github.kamilszewc'
version '4.20'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Testing
    testImplementation 'org.apache.groovy:groovy-all:4.0.8'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'

    // Fixing vulnerability
    implementation 'org.testng:testng:7.7.0'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            try {
                credentials {
                    username = "$mavenCentralUser"
                    password = "$mavenCentralPassword"
                }
            } catch (Exception ex) {
                credentials {
                    username = " "
                    password = " "
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'resourcewatcher'
            from components.java
            pom {
                name = 'Resource Watcher'
                description = 'Java-based Linux, Mac, Windows resources watcher (cpu, gpu, ram, disk, ...)'
                url = 'https://github.com/computinglaboratory/resourcewatcher'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/resourcewatcher.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/resourcewatcher.git'
                    url = 'https://github.com/kamilszewc/resourcewatcher.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}